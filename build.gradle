apply plugin: "base"

//~~~~~~~~~~~~~~~~~~~~~ Properties And Configs ~~~~~~~~~~~~~~~~~~~~~

group = "sunlaud.firetabs"
version = "0.3"
description = "Firefox extension that collects all open tabs into one document"


ext {
    sources = 'src/addon'
    appRoot = "${sources}/chrome"
    appDirs = ['skin/','content/','locale/']
    additionalFiles = ['LICENSE']
    explodedXpi = "${buildDir}/${project.name}"
}


//~~~~~~~~~~~~~~~~~~~~~ Tasks ~~~~~~~~~~~~~~~~~~~~~


task appJar(type: Zip) {
    from appRoot
    include(appDirs)
    destinationDir = file("${explodedXpi}/chrome")
    extension = 'jar'
    version = null
}

task manifest(type:Copy) {
    from "${sources}/chrome.manifest"
    filter { line -> line.replaceAll("\\s(${appDirs.join('|')})", { match -> "\tjar:chrome/${project.name}.jar!/" + match[1] }) }
    into explodedXpi
}

task mainIcon(type:Copy) {
    from "${appRoot}/skin/classic/firetabs-64.png"
    into explodedXpi
    rename '.*', 'icon.png'
}

task xpiExploded(type:Copy, dependsOn: [appJar, manifest, mainIcon]) {
    from sources
    include 'install.rdf'
    include 'defaults/'
    from(additionalFiles) {
        include '*'
    }
    into explodedXpi
    includeEmptyDirs = false
}

task xpi(type: Zip) {
    from xpiExploded
    extension = 'xpi'
}





