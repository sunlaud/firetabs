apply plugin: "base"

//~~~~~~~~~~~~~~~~~~~~~ Properties And Configs ~~~~~~~~~~~~~~~~~~~~~

group = "sunlaud.firetabs"
version = "0.3"
description = "Firefox extension that collects all open tabs into one document"


ext {
    sources = 'src/addon'
    appRoot = "${sources}/chrome"
    appDirs = ['skin/','content/','locale/']
    additionalFiles = ['LICENSE']
    explodedXpi = "${buildDir}/${project.name}"
    mainIcon = "${appRoot}/skin/classic/firetabs-64.png"
}


//~~~~~~~~~~~~~~~~~~~~~ Tasks ~~~~~~~~~~~~~~~~~~~~~


task appJar(type: Zip) {
    from appRoot
    include(appDirs)
    destinationDir = file("${explodedXpi}/chrome")
    extension = 'jar'
    version = null
}

task manifest(type: Copy) {
    from "${sources}/chrome.manifest"
    into explodedXpi
    filter { line -> line.replaceAll("\\s(${appDirs.join('|')})", { match -> "\tjar:chrome/${project.name}.jar!/" + match[1] }) }
}

task icon(type: Copy) {
    from mainIcon
    into explodedXpi
    rename '.*', 'icon.png'
}

task xpiExploded(type: Copy, dependsOn: [appJar, manifest, icon]) {
    from sources
    from(additionalFiles) {
        include '*'
    }
    into explodedXpi
    include 'install.rdf'
    include 'defaults/'
    includeEmptyDirs = false
}

task xpi(type: Zip) {
    from xpiExploded
    extension = 'xpi'
    doLast {
        ant.checksum file: archivePath, algorithm: "SHA-1"
    }
}

