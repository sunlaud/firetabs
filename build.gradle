apply plugin: "base"

//~~~~~~~~~~~~~~~~~~~~~ Properties And Configs ~~~~~~~~~~~~~~~~~~~~~

group = "sunlaud.firetabs"
version = "0.3"
description = "Firefox extension that collects all open tabs into one document"


ext {
    appDirs = ['skin/','content/','locale/']
    additionalFiles = ['readme.txt']
    explodedXpi = "${buildDir}/${project.name}"
    sources = 'src'
}


//~~~~~~~~~~~~~~~~~~~~~ Tasks ~~~~~~~~~~~~~~~~~~~~~


task appJar(type: Zip) {
    from sources
    include(appDirs)
    destinationDir = file("${explodedXpi}/chrome")
    extension = 'jar'
    version = null
}

task manifest(type:Copy) {
    from sources
    include 'chrome.manifest'
    filter { line -> line.replaceAll("\\s(${appDirs.join('|')})", { match -> "\tjar:chrome/${project.name}.jar!/" + match[1] }) }
    into explodedXpi
}

task xpiContents(type:Copy, dependsOn: [appJar, manifest]) {
    from sources
    include 'install.rdf'
    include(additionalFiles)
    into explodedXpi
    includeEmptyDirs = false
}

task xpi(type: Zip) {
    from xpiContents
    extension = 'xpi'
}





